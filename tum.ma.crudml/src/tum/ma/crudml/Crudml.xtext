grammar tum.ma.crudml.Crudml with org.eclipse.xtext.common.Terminals

generate crudml "http://www.ma.tum/crudml/Crudml"

Domainmodel:
  (elements += AbstractElement)*
;
 
/* Package & import block */ 
PackageDeclaration:
  'package' name = QualifiedName '{'
    (elements += AbstractElement)*
  '}'
;
 
AbstractElement:
  PackageDeclaration | Entity | Import | Right | Role | Metadata | Persistence
;
 
QualifiedName:
  ID ('.' ID)*
;
 
Import:
  'import' importedNamespace = QualifiedNameWithWildcard
;
  
QualifiedNameWithWildcard:
  QualifiedName '.*'?
;

Metadata:
	'metadata' 
	'{'
	  (entries += MetadataEntry)*
	'}'
;

MetadataEntry:
	(type = ('workspace' | 'applicationName' | 'author' )) ':' value = ID
;

Persistence:
	'persistence' 
	'{'
	  (entries += PersistenceEntry)*
	'}'
;

PersistenceEntry:
	DBConnectionDefinition | DBTypeDefiniton | DBSetupDefinition
;

DBConnectionDefinition:
	(type = ('user' | 'password' | 'path') ':' value = ID)
;

DBTypeDefiniton:
	'type' ':' value = ('derby')
;

DBSetupDefinition:
	'setup' ':' value = ('dropAndCreate' | 'none')
;
  
/* Entity block */
Entity:
  (annotations += Annotation)*	
  'entity' name = ID             
  '{'
    (attributes += Attribute)*
  '}'
;

/* Annotation block */
Annotation:
  '@' UiLevel
;

UiLevel:
	'UiLevel'level = INT
;

/* Attribute block */
Attribute:
	Reference | Member
;

Member:
	name = ID ':' (type = [Entity | QualifiedName] | primitive = ('string' | 'int' | 'boolean' | 'long' | 'double'))
;

Reference:
	reftype = ('many' | 'one') name = ID ':' type = [Entity]
;

/* Operations block */
Operations:
	('all' | (Operation)+)
;

Operation:
	'create' | 'read' | 'update' |'delete'
;

/* Rights and access control */

Role:
  'role' name = ID             
  '{'
  	('init' initialization = ('assigned' | 'first'))?
    (rights += [Right] | 'all')*
  '}'
;

Right:
  'right' name = ID             
  ('{'
    (accesses += Access)*
  '}')?
;

Access:
	type = [Entity | QualifiedName] ':' operations = Operations
;
